import com.github.jengelman.gradle.plugins.shadow.internal.DependencyFilter
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        jcenter()
        maven {
            name = 'xapiLocal'
            url = new URI("file://$rootDir/repo")
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
        classpath 'net.wti.gradle.tools:xapi-gradle-tools:0.5.1'
    }
}

setProperty('xapi.main.component', 'shadow')
apply from: "$rootDir/gradle/xapi-env.gradle"
plugins.apply 'com.github.johnrengelman.shadow'
plugins.apply 'xapi-require'

(tasks.jar as Jar).archiveClassifier.set 'default'

tasks.named('shadowJar').configure {
    ShadowJar jar ->
        jar.classifier = null
        jar.zip64=true
        jar.minimize {
            DependencyFilter filter ->
                filter.exclude {
                    ResolvedDependency res ->
                        if (res.moduleGroup =~ '(' +
                                'org.assert|' +
                                'junit|' +
                                'org.junit|' +
                                'org.hamcrest|' +
                                'javax.enterprise|' +
                                'javax.servlet|' +
                                'commons-coded|' +
                                'com.google.guava' +
                                ').*') {
                            return false
                        }
                        switch(res.moduleName) {
                            case 'gwt-dev':
                            case 'gwt-user':
                            case 'elemental':
                                return false
                        }
//                        println ""
//                        println res.name
//                        println res.moduleName
                        return true
                }
        }
}
artifacts {
    archives shadowJar
}
assemble.dependsOn(shadowJar)

xapiRequire.main().configure {
    req ->
        req.external "net.wti.core:xapi-gen:$version", 'main'
        req.project 'xapi-server-parent:xapi-server-vertx'
        req.project 'xapi-jre-parent:xapi-jre-ui-parent:xapi-jre-ui-javafx'
        req.project 'xapi-inject', 'jre:main'
        req.project 'xapi-process', 'jre:main'
        req.project 'xapi-dev-parent:xapi-dev-file'
        req.project 'xapi-dev-parent:xapi-dev-shell'
        req.project 'xapi-dev-parent:xapi-dev-scanner'
        req.project 'xapi-dev-gwtc:xapi-gwtc-api'
        req.project 'xapi-dev-parent:xapi-dev-gwtc:xapi-gwtc-impl', 'gwt:main'
        req.project 'xapi-dev-parent:xapi-dev-maven'
        req.project 'xapi-server-parent:xapi-server-gen'
        req.external 'javax.inject:javax.inject:1:sources'

}

xapiRequire.module('test').configure {
    req ->
        req.project 'xapi-common', 'test'

}

description = 'XApi - Dev uber jar'
