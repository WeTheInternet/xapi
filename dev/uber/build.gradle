import com.github.jengelman.gradle.plugins.shadow.internal.DependencyFilter
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        gradlePluginPortal()
        mavenLocal()
        maven {
            name = 'xapiLocal'
            url = new URI("file://$rootDir/repo")
        }
    }
    dependencies {
        if (gradle.gradleVersion.startsWith("5")) {
            classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
        } else {
            // this is a hacked version where we allow any gradle version and build in java 8
            classpath 'com.gradleup.shadow:com.gradleup.shadow.gradle.plugin:8.3.5-SNAPSHOT'
        }

        classpath 'net.wti.gradle.tools:xapi-gradle-tools:0.5.1'
    }
}
//plugins {
//    id 'com.gradleup.shadow' version '8.3.3'
//}
setProperty('xapi.main.component', 'shadow')
apply from: "$rootDir/gradle/xapi-env.gradle"


//plugins.apply 'com.github.johnrengelman.shadow'
if (gradle.gradleVersion.startsWith("5")) {
    plugins.apply 'com.github.johnrengelman.shadow'
} else {
    plugins.apply 'java-library'
    plugins.apply 'com.gradleup.shadow'
}
plugins.apply 'xapi-require'

(tasks.jar as Jar).archiveClassifier.set 'default'

tasks.named('shadowJar').configure {
    ShadowJar jar ->
        jar.archiveClassifier.set(null)
        jar.zip64=true
        jar.minimize {
            DependencyFilter filter ->
                filter.exclude {
                    ResolvedDependency res ->
                        if (res.moduleGroup =~ '(' +
                                'org.assert|' +
                                'junit|' +
                                'org.junit|' +
                                'org.hamcrest|' +
                                'javax.enterprise|' +
                                'javax.servlet|' +
                                'commons-coded|' +
                                'com.google.guava' +
                                ').*') {
                            return false
                        }
                        switch(res.moduleName) {
                            case 'gwt-dev':
                            case 'gwt-user':
                            case 'elemental':
                                return false
                        }
//                        println ""
//                        println res.name
//                        println res.moduleName
                        return true
                }
        }
}
artifacts {
    archives shadowJar
}
assemble.dependsOn(shadowJar)

xapiRequire.main().configure {
    req ->
        req.external "net.wti.core:xapi-gen:$version", 'main'
        req.project 'xapi-server-parent:xapi-server-vertx'
        req.project 'xapi-jre-parent:xapi-jre-ui-parent:xapi-jre-ui-javafx'
        req.project ':inject-jre'
        req.project ':process-jre'
        req.project ':dev:file-main'
        req.project 'xapi-dev-parent:xapi-dev-shell'
        req.project ':dev:scanner-main'
        req.project ':gwtc-api'
        req.project ':gwtc-gwtCompiler'
        req.project ':dev:maven-main'
        req.project ':server:gen-main'
        req.external 'javax.inject:javax.inject:1:sources'

}

xapiRequire.module('test').configure {
    req ->
        req.project ':base-testTools'
}

description = 'XApi - Dev uber jar'

tasks.withType(Test).configureEach {
    Test t ->
        t.maxHeapSize("4G")
        t.forkEvery(1)
        t.maxParallelForks(4)
}