import com.github.jengelman.gradle.plugins.shadow.internal.DependencyFilter
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        jcenter()
        maven {
            name = 'xapiLocal'
            url = new URI("file://$rootDir/repo")
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath 'net.wti.gradle:xapi-gradle-plugin:0.5.1'
    }
}

plugins.apply 'com.github.johnrengelman.shadow'
setProperty('xapi.main.component', 'shadow')
apply from: "$rootDir/gradle/xapi-env.gradle"

(tasks.jar as Jar).archiveClassifier.set 'default'

tasks.named('shadowJar').configure {
    ShadowJar jar ->
        jar.classifier = null
        jar.zip64=true
        jar.minimize {
            DependencyFilter filter ->
                filter.exclude {
                    ResolvedDependency res ->
                        if (res.moduleGroup =~ '(' +
                                'org.assert|' +
                                'junit|' +
                                'org.junit|' +
                                'org.hamcrest|' +
                                'javax.enterprise|' +
                                'javax.servlet|' +
                                'commons-coded|' +
                                'com.google.guava' +
                                ').*') {
                            return false
                        }
                        switch(res.moduleName) {
                            case 'gwt-dev':
                            case 'gwt-user':
                            case 'elemental':
                                return false
                        }
//                        println ""
//                        println res.name
//                        println res.moduleName
                        return true
                }
        }
}
artifacts {
    archives shadowJar
}
assemble.dependsOn(shadowJar)

dependencies {
    compile project(':xapi-server-parent:xapi-server-vertx')
    compile project(':xapi-jre-parent:xapi-jre-ui-parent:xapi-jre-ui-javafx')
    compile project(':xapi-jre-parent:xapi-jre-inject')
    compile project(':xapi-jre-parent:xapi-jre-process')
    compile project(':xapi-dev-parent:xapi-dev-file')
    compile project(':xapi-dev-parent:xapi-dev-shell')
    compile project(':xapi-dev-parent:xapi-dev-scanner')
    compile project(':xapi-dev-parent:xapi-dev-gwtc:xapi-gwtc-api')
    compile project(':xapi-dev-parent:xapi-dev-gwtc:xapi-gwtc-impl')
    compile project(':xapi-dev-parent:xapi-dev-maven')
    compile "net.wti.core:xapi-gen:$version"
    compile project(':xapi-server-parent:xapi-server-gen')
    testCompile project(':xapi-core-parent:xapi-core-test')
}

description = 'XApi - Dev uber jar'
