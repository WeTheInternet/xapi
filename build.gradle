plugins {
    id 'groovy'
    id 'idea'
}

import net.wti.gradle.system.spi.GradleServiceFinder

group = 'net.wetheinter'

apply from: "$rootDir/gradle/xapi-env.gradle"

version = findProperty('xapiVersion')

tasks.register 'publishRequired', { req ->
    allprojects {
        def pub = tasks.findByName('xapiPublish')
        if (pub) {
            req.dependsOn pub
        }
    }
}

File mvnRepo = new File(rootDir, 'repo')
//File indexRoot = new File(rootDir, 'build/xindex')
File buildRoot = new File(rootDir, 'build')


gradle.beforeProject {
    Project p ->
        p.java.toolchain.languageVersion.set(JavaLanguageVersion.of(8))
        if (p.group != 'net.wetheinter') {
            p.logger.info "gradle.beforeProject group was: $p.group"
            p.group = 'net.wetheinter'
        }

        if (p.buildscript.repositories.empty) {
            p.buildscript.repositories.maven {
                name = 'xapiLocal'
                url = new File(rootDir, 'repo')
            }
            // TODO make this obsolete by priming all external dependencies to xapiLocal
            p.buildscript.repositories.mavenCentral()
        }
}
gradle.beforeProject {
    Project p ->
        if (p.group != 'net.wetheinter') {
            p.logger.info "allprojects group was $p.group"
            p.group = 'net.wetheinter'
        }
        p.version = findProperty('xapiVersion')

        p.repositories {
            maven {
                name = 'xapiExternal'
                File repo = new File("$project.rootDir/repoMvn".toString())
                if (!repo.exists()) {
                    repo = new File("$project.rootDir.parent/xapi/repoMvn".toString())
                }
                url = repo.toURI()
                metadataSources { sources ->
                    sources.mavenPom()
                }
            }
            mavenCentral()
        }
        p.with {


            tasks.withType(JavaCompile) {
                options.encoding = 'UTF-8'
                // explicitly empty sourcepath.
                // If we want to recompile anything that isn't ours, we'll do so explicitly.
                options.sourcepath = files()
            }

            tasks.withType(Test).configureEach {
                Test test ->
                    test.systemProperty('xapi.mvn.repo', mvnRepo.absolutePath)
                    if (!test.maxHeapSize) {
                        test.maxHeapSize = '1G'
                    }

                    String compileForTest = "compile${test.name - "test" - "Main"}TestJava"
                    JavaCompile javac = project.tasks.findByName(compileForTest)
                    if (javac) {
                        File metaDir = javac.destinationDir
                        test.systemProperty('xapi.injector.cache', metaDir.absolutePath)
                        test.doFirst {
                            new File(metaDir, 'META-INF/singletons').mkdirs()
                            new File(metaDir, 'META-INF/instances').mkdirs()
                        }
                    } else {
                        test.logger.info "Could not find $compileForTest in $project.path"
                    }
            }
        return
    }
}

//GradleServiceFinder.getService(project).configureWrapper(project)

wrapper {
    gradleVersion = "8.11.1"
    distributionType = 'ALL'
}

idea {
    module {
        excludeDirs = [mvnRepo, buildRoot] as Set
    }
}

tasks.register('projectInfo') {
    Task t ->
        t.doLast {
            t.logger.quiet("Total projects: " + allprojects.size())
            // perhaps get some xapi schema stats about realized modules to print here?
        }
}