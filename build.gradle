plugins {
    id 'groovy'
    id 'idea'
    id 'xapi-root'
}

import org.gradle.internal.jvm.Jvm

import static org.gradle.api.tasks.wrapper.Wrapper.DistributionType

apply from: "$rootDir/gradle/xapi-env.gradle"

plugins.apply 'xapi-root'


xapi {
    preload('javax.validation:validation-api:1.0.0.GA')
    preload('javax.validation:validation-api:1.0.0.GA:sources')
}

import xapi.gradle.gwt.Gwt
File mvnRepo = new File(rootDir, 'repo')

def shouldApply = { String path->
    return ! (
            (path.split(':')?:[''])[-1]
                    .contains("parent")
            )
}

allprojects {
    group = 'net.wetheinter'
    version = findProperty('xapiVersion')

    plugins.apply 'maven'
    plugins.apply 'idea'
    repositories.jcenter()
    repositories.maven {
        name = 'xapiLocal'
        url = mvnRepo.toURI()
    }
    // we want all projects to have xapi plugin,
    // but we'll wait until they are resolved,
    // to give each project a chance to pick a more specific plugin.
    afterEvaluate {
        if (shouldApply(path)) {
            plugins.apply 'xapi-base'
        }
        test {
            if (!maxHeapSize) {
                maxHeapSize = '1G'
            }
        }
    }
}

gradle.beforeProject {
    Project p ->
        if (p.buildscript.repositories.empty) {
            p.buildscript.repositories.jcenter()
            p.buildscript.repositories.maven {
                name = 'xapiLocal'
                url = new File(rootDir, 'repo')
            }
        }
}

subprojects {
    Project p ->
    if (!shouldApply(p.path)) {
        return
    }
    p.with {
        plugins.apply 'java-library'

        tasks.create 'testJar', Jar, {
            Jar jar ->
                jar.classifier = 'test'
                jar.from sourceSets.test.output
        }
        Gwt.maybeInstall(p)

        p.dependencies.add JavaPlugin.RUNTIME_ONLY_CONFIGURATION_NAME,
                p.files(Jvm.current().toolsJar)

        p.tasks.withType(Test).all {
            Test test ->
                test.systemProperty('xapi.mvn.repo', mvnRepo.absolutePath)
        }

        p.tasks.withType(JavaCompile).all {
            JavaCompile javac ->
                // explicitly empty sourcepath.
                // If we want to recompile anything that isn't ours, we'll do so explicitly.
                javac.options.sourcepath = files()
        }

        artifacts {
            testRuntime testJar
        }

        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }
    }
}

tasks.named('wrapper', {
    Wrapper w -> w.with {
        gradleVersion = '5.0'
        distributionType = DistributionType.ALL
    }
})
