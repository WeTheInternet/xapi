plugins {
    id 'groovy'
    id 'idea'
}

import net.wti.gradle.system.spi.GradleServiceFinder
import org.gradle.internal.jvm.Jvm

apply from: "$rootDir/gradle/xapi-env.gradle"

tasks.create 'publishRequired', { req ->
    allprojects {
        def pub = tasks.findByName('xapiPublish')
        if (pub) {
            req.dependsOn pub
        }
    }
}

File mvnRepo = new File(rootDir, 'repo')

def shouldApply = { Project p->
    return ! (
            p.plugins.hasPlugin('xapi-schema') ||
            p.plugins.hasPlugin('xapi-require') ||
            (p.path.split(':')?:[''])[-1]
                    .contains("parent")
            )
}

allprojects {
    Project p -> p.with {
        group = 'net.wetheinter'
        version = findProperty('xapiVersion')

        plugins.apply 'idea'
        plugins.apply 'xapi-schema'

        // TODO: remove this by priming xapiLocal repo with all external dependencies
        repositories.jcenter()

    }
        
}

gradle.beforeProject {
    Project p ->
    if (p.buildscript.repositories.empty) {
        p.buildscript.repositories.maven {
            name = 'xapiLocal'
            url = new File(rootDir, 'repo')
        }
        // TODO make this obsolete by priming all external dependencies to xapiLocal
        p.buildscript.repositories.jcenter()
    }
}

subprojects {
    Project p ->

        p.with {
            sourceCompatibility = '1.8'
            targetCompatibility = '1.8'


            tasks.withType(JavaCompile) {
                options.encoding = 'UTF-8'
                // explicitly empty sourcepath.
                // If we want to recompile anything that isn't ours, we'll do so explicitly.
                options.sourcepath = files()
            }

            tasks.withType(Test).all {
                Test test ->
                    test.systemProperty('xapi.mvn.repo', mvnRepo.absolutePath)
                    File metaDir = (tasks.compileTestJava as JavaCompile).destinationDir
                    test.systemProperty('xapi.meta', metaDir.absolutePath)
//                    test.systemProperty('xapi.injector.cache', metaDir.absolutePath)
                    if (!test.maxHeapSize) {
                        test.maxHeapSize = '1G'
                    }
                    test.doFirst {
                        new File(metaDir, 'META-INF/singletons').mkdirs()
                        new File(metaDir, 'META-INF/instances').mkdirs()
                    }
            }

            if (path != ':schema') {
                // we don't actually need the java plugin, so we leave it out, and just turn it on once in a while for manual verification
//                plugins.apply 'java'
                plugins.apply 'xapi-schema'
                plugins.apply 'maven-publish'
                plugins.apply 'xapi-publish'


                tasks.create 'testJar', Jar, {
                    Jar jar ->
                        jar.archiveClassifier.set 'test'
                        jar.from sourceSets.test.output
                }

//        Gwt.maybeInstall(p)

                File tools = Jvm.current().toolsJar
                if (tools) {
                    p.dependencies.add JavaPlugin.RUNTIME_ONLY_CONFIGURATION_NAME,
                            p.files(tools)
                }

                artifacts {
                    testRuntime testJar
                }
            }

    }
}

GradleServiceFinder.getService(project).configureWrapper(project)


idea {
    module {
        excludeDirs = [mvnRepo] as Set
    }
}
