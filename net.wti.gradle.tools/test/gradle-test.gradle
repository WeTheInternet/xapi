plugins {
    id 'java'
    id 'groovy'
}

description = '''Gradle testing tools (test dependencies in api configuration).'''

String xapiHome = rootDir.parent
apply from: "$xapiHome/gradle/xapi-env.gradle"
apply from: "$xapiHome/gradle/int-test.gradle"

dependencies {
    compile project(':xapi-gradle-core')
    compile gradleApi()
    compile gradleTestKit()
    compile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    compile 'junit:junit:4.12' // TODO: get junit 5 vintage mode to work
    compile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude module: 'groovy-all'
    }
}

configurations {
    apiElements {
        outgoing.variants.getByName('classes').artifact(
                file: compileGroovy.destinationDir,
                type: ArtifactTypeDefinition.JVM_CLASS_DIRECTORY,
                builtBy: compileGroovy)
    }
}

sourceSets {
    main {
        java {
            srcDirs = []
        }
        groovy { 
            srcDirs = ['src/main/java', 'src/main/groovy']
        }
    }
}