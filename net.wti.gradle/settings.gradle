rootProject.name = 'net.wti.gradle'

List<String> projects = [
        'api', 'mirror', 'plugin', 'meta', 'manifest', 'loader'
]
include(projects as String[])

def artifactId = {
    switch (it) {
        // TODO: move all the rest into here and then make it the default.
        // Requires moving source, and git backlog is already really big;
        // We're just going to do the minimum required to get the annotation mirror processor working.
        case 'mirror':
            return "dev-$it"
    }
    return "gradle-$it"
}

projects.forEach({p->
    project(":$p").buildFileName = "dev-${p}.gradle"
    project(":$p").name = "xapi-${artifactId(p)}"
})

def composite = System.getProperty('xapi.gradle.composite') ?:
        startParameter.projectProperties.get('xapi.gradle.composite')
if (composite == 'true') {
    logger.quiet "Adding composite from net.wti.gradle to net.wti.core because xapi.gradle.composite=$composite"
    System.setProperty('xapi.gradle.composite', 'false')
    includeBuild('../net.wti.gradle.tools')
    includeBuild('../net.wti.core')
}

enableFeaturePreview('GRADLE_METADATA')