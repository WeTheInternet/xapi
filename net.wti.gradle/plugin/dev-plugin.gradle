plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'java-library'
}
String xapiHome = "$rootDir.parent"
apply from: "$xapiHome/gradle/xapi-env.gradle"
apply from: "$xapiHome/gradle/int-test.gradle"
repositories.jcenter()

def module = project.dependencies.&create
def local = { path -> project.dependencies.project(path: path) }
def depend = { String key, Object item ->
    if (item instanceof Iterable) {
        for (Object i : item) {
            project.dependencies.add(key, i)
        }
    } else {
        project.dependencies.add(key, item)
    }
}

depend('api', [
        local(':xapi-gradle-api'),
        module("net.wti.core:xapi-gen:$version".toString()),
        module("net.wti.core:xapi-fu:$version".toString()),
        module("net.wti.gradle.tools:xapi-gradle-tools:$version".toString())
])
depend 'compileOnly', [
        project.dependencies.gradleApi(),
        // we'll leave it up to scripts to decide which plugin/version they want to use...
        module('com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'),
        module('org.jfrog.buildinfo:build-info-extractor-gradle:4.8.1'),
        module('com.github.jengelman.gradle.plugins:shadow:4.0.4')
]
depend('testImplementation', [
        module("net.wti.gradle.tools:xapi-gradle-test:$version".toString())
])

tasks.withType(Test).configureEach({
    Test t ->
        t.systemProperty 'xapi.home', xapiHome
})